apply plugin: "maven"
apply plugin: "propdeps-maven"

install {
    repositories.mavenInstaller {
        customizePom(pom, project)
    }
}

def customizePom(pom, gradleProject) {
    pom.whenConfigured { generatedPom ->

        // eliminate test-scoped dependencies (no need in maven central poms)
        generatedPom.dependencies.removeAll { dep ->
            dep.scope == 'test'
        }

        // add all items necessary for maven central publication
        generatedPom.project {
            name = gradleProject.description
            description = gradleProject.description
            url = 'http://github.com/SpringSource/spring-hadoop'
            organization {
                name = 'SpringSource'
                url = 'http://www.springsource.org/spring-data/hadoop'
            }
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                url = 'http://github.com/SpringSource/spring-hadoop'
                connection = 'scm:git:git://github.com/SpringSource/spring-hadoop'
                developerConnection = 'scm:git:git://github.com/SpringSource/spring-hadoop'
            }
            developers {
                developer {
                    id = 'costin'
                    name = 'Costin Leau'
                    email = 'cleau@vmware.com'
                    properties {
                        twitter = 'costinl'
                    }
                }
            }
        }
    }
}